apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'A non-official Android Framework for Editor.js - block styled editor'
                    url = 'https://github.com/Upstarts/editor.js-kit-android'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/Upstarts/editor.js-kit-android/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'raliev'
                            name = 'Ruslan Aliev'
                            email = 'r.aliev@upstarts.work'
                        }
                    }

                    scm {
                        connection = 'scm:github.com/Upstarts/editor.js-kit-android.git'
                        developerConnection = 'scm:git:ssh://github.com/Upstarts/editor.js-kit-android.git'
                        url = 'https://github.com/Upstarts/editor.js-kit-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
